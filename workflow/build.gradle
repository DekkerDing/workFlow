plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.6.14'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

description '工作流引擎'

apply from: 'dependencies.gradle'

group = 'io.github.DekkerDing'
version = '1.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding "UTF-8"
compileTestJava.options.encoding "UTF-8"

repositories {
    mavenLocal()
    gradlePluginPortal()
    maven { url 'https://repo.springsource.org/plugins-release' }
    maven { url 'https://repo.spring.io/plugins-release-local' }
    maven { url "https://repo.spring.io/plugins-release" }
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://maven.aliyun.com/repository/goole' }
    maven { url 'https://maven.aliyun.com/repository/spring' }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/libs-snapshot' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://repo.spring.io/libs-spring-framework-build" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'https://repo.spring.io/plugins-snapshot' }
    maven { url 'https://plugins.gradle.org/m2/' }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    dependencies {
        for (version in rootProject.ext.libraries) {
            dependency version.value
        }
    }

    imports {
        for (imports in rootProject.ext.imports) {
            mavenBom imports.value
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

javadoc {
    failOnError false
    afterEvaluate {
        configure(options) {
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
            failOnError false
            links "http://docs.oracle.com/javase/8/docs/api"
        }
    }
}

jar {
    enabled = true
}

bootJar {
    enabled = false
}

idea {
    project {
        jdkName = "1.8"
        languageLevel = "1.8"
        vcs = "Git"
    }
}

configurations.all() {
    Configuration configuration ->
        configuration.resolutionStrategy.failOnVersionConflict()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}
